buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.0.0'
    testCompile 'junit:junit:4.12'
}

sourceSets {
    main {
        proto {
            srcDir '../lib/proto'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }

    generatedFilesBaseDir = "$projectDir/gen"
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file("../lib/proto");
    }
}

task compileRust(type: Exec) {
    commandLine 'cargo', 'build', '--target-dir', "${buildDir}/cargo"
}

classes.dependsOn compileRust

test {
    systemProperty "java.library.path", file("${buildDir}/cargo/debug").absolutePath
}
